// A path is a series of dot-separated identifiers and [] based item-access.
start: [(_identifier | _getitem) (("." _identifier) | _getitem)*]
?key: number   // item-access keys can be any hashable python literal
    | BOOLEAN
    | NONE
    | STRING
    | slice_key
    | tensor_slice_key
    | tuple_key

_getitem: "[" key "]"

tuple_key: "()"
         | "(" key ",)"
         | "(" key ("," key)+ [","] ")"

number: DEC_NUMBER
      | HEX_NUMBER
      | BIN_NUMBER
      | OCT_NUMBER
      | FLOAT_NUMBER
      | COMPLEX_NUMBER

?tensor_axis_key.2: number | slice_key | ELLIPSIS | NONE
tensor_slice_key.2:  tensor_axis_key ","
                  | tensor_axis_key  ("," tensor_axis_key )+ [","]
!slice_key: [number] ":" [number]
          | [number] ":" [number] ":" [number]

_identifier: STAR | DOUBLE_STAR | IDENTIFIER | DEC_NUMBER

NONE: "None"
BOOLEAN: "True" | "False"
ELLIPSIS: "..."

STAR: "*"
DOUBLE_STAR: "**"
IDENTIFIER: /[^\W\d]\w*/
STRING: /(u?|r?b?|br)("(?!"").*?(?<!\\)(\\\\)*?"|'(?!'').*?(?<!\\)(\\\\)*?')/i
DEC_NUMBER: /[+-]?\d+/
HEX_NUMBER.2: /[+-]?0x[\da-f]*/i
OCT_NUMBER.2: /[+-]?0o[0-7]*/i
BIN_NUMBER.2 : /[+-]?0b[0-1]*/i
FLOAT_NUMBER: /[+-]?((\d+\.\d*|\.\d+|\d+)(e[-+]?\d+)?|\d+(e[-+]?\d+))/i
IMAG_NUMBER: (DEC_NUMBER | FLOAT_NUMBER) "j"i
COMPLEX_NUMBER: IMAG_NUMBER
              | "(" (FLOAT_NUMBER | DEC_NUMBER) /[+-]/ IMAG_NUMBER ")"

%import common.WS_INLINE
%ignore WS_INLINE